
! Description: barotropic stream function, documentation at bsf.rst
!
!       Usage: ferret bsf.jnl soda/licom/pcom_n/pcom_r
!
!      Author: OU Yuyuan <ouyuyuan@lasg.iap.ac.cn>
!     Created: 2014-03-17 14:15:30 BJT
! Last Change: 2014-04-07 21:17:20 BJT

let model = "$1"
let data_dir = "/home/ou/archive/data"

if `model eq "soda"` then
   let in_dir = "`data_dir`/soda/climate"
   let infile = "`in_dir`/climate_1981-2000.nc"
elif `model eq "licom"` then
   let in_dir = "`data_dir`/licom/climate/unmodified"
   let infile = "`in_dir`/climate_081-100.nc"
!   let infile = "`in_dir`/climate_081-100_positive_level.nc"
elif `model eq "pcom_n"` then
   let in_dir = "`data_dir`/pcom/climate/fix_fkh"
   let infile = "`in_dir`/year_mean_081-100.nc"
elif `model eq "pcom_r"` then
   let in_dir = "`data_dir`/pcom/climate/res_s"
   let infile = "`in_dir`/year_mean_081-100.nc"
else
   say Unkown model
   exit
endif
let outfile = "`in_dir`/calculated/bsf_ferret.nc"
!let outfile = "`in_dir`/calculated/bsf.nc"

set data "`infile`"

if `model eq "licom"` then
   let u_ver = us[i=1:360,z=@din] 
else 
   let u_ver = u[z=@din]
endif

let uu = missing(u_ver, 0)

! u_ver - u_ver will be a mask that contain missing value
! missing value of bsf should be the same as u_ver
let bsf = (uu[y=@din] - uu[y=@iin])*10^(-6) + (u_ver - u_ver)

set var/title="Barotropic Stream Function"/units="Sv" bsf
cancel mode upcase_output
save/clobber/file="`outfile`" bsf
