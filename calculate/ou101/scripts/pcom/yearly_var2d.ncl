
; Description: extract and catenate var2d from yearly files
;
;      Author: OU Yuyuan <ouyuyuan@lasg.iap.ac.cn>
;     Created: 2014-10-24 19:43:19 BJT
; Last Change: 2014-12-05 21:36:42 BJT

load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

function get_2d(f, varName)
begin
  var2d0 = nc->$varName$(0,:,:)
  var2d0@_FillValue = default_fillvalue(typeof(var2d0))
  var2d0 = where(var2d0.gt.10e10, var2d0@_FillValue, var2d0)
  delete(var2d0@missing_value)
  return var2d0 
end

function create_var2d(var2d0)
begin
  dims = dimsizes(var2d0)
  nlat = dims(0)
  nlon = dims(1)
  var2d = new((/ny, nlat, nlon/), typeof(var2d0))
  copy_VarAtts(var2d0, var2d)
  var2d!1 = "lat"
  var2d!2 = "lon"
  var2d&lat = var2d0&lat
  var2d&lon = var2d0&lon
  return var2d
end

datDir = getenv("NCL_ARG_1")
yb = 501
ye = 505
ny = ye-yb+1

time = new(ny, integer)
time@units = "years since 0500-01-01"
time@long_name = "time"

do yr = yb, ye
  ifname = datDir+sprinti( "year_%0.4i.nc", yr )
  nc = addfile(ifname, "r")
  bcu_an0 = get_2d(nc, "bcu_an")
  delete(nc)
  time(yr-yb) = yr

  if (yr .eq. yb) then
    bcu_an = create_var2d(bcu_an0)
  end if
  bcu_an(yr-yb,:,:) = bcu_an0
end do

bcu_an!0 = "time"
bcu_an&time = time

outfile = datDir+"post/"+getenv("NCL_ARG_0_base")+".nc"
system("rm -f " + outfile)
f = addfile(outfile, "c")
filedimdef(f, "time", -1, True)
f->bcu_an = bcu_an
f@source = "create by " + systemfunc("pwd") + "/" + getenv("NCL_ARG_0") + \
", " + systemfunc("date")
delete(f)
