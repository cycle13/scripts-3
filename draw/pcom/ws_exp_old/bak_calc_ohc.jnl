let outfile = "$1"
cancel mode upcase_output
let rho = 1029 ! kg/m^3, mean density of seawater
let cp = 3901 ! c_p, heat capability, J/kg/K

let indir = "/home/ou/archive/data/pcom/"
let clm = "year_501-550.nc"

let infile = "`indir`/basin/basin.nc"
set data "`infile`"
let mask = basin[d=1]

! control run, ohc by depth
!===========================
set data = "`indir`/exp08_fkh1/`clm`"

let ctr_all = rho*cp*pt[x=@din, y=@din, z=@din]
set var/title="global, all depth, control run "/units="J" ctr_all
save/clobber/file="`outfile`" ctr_all

let ctr_2000 = rho*cp*pt[x=@din, y=@din, z=0:2000@din]
set var/title="global, 0-2000m, control run "/units="J" ctr_2000
save/append/file="`outfile`" ctr_2000

let ctr_700 = rho*cp*pt[x=@din, y=@din, z=0:700@din]
set var/title="global, 0-700m, control run "/units="J" ctr_700
save/append/file="`outfile`" ctr_700

! control run, ohc above 20 degC
!================================
let temp = if pt ge 20 then pt else 0

! global
let varmask = 1
let title = "global, above 20 degC"
go func_ohc_save ctr_g

! Indian ocean
let varmask = if mask eq 3 then 1 else 0 
let title = "Indian Ocean"
go func_ohc_save ctr_i 

! Pacific ocean
let varmask = if mask eq 2 then 1 else 0 
let title = "Pacific Ocean"
go func_ohc_save ctr_p 

! Atlantic ocean
let varmask = if mask eq 6 then 1 else 0 
let title = "Atlantic Ocean"
go func_ohc_save ctr_a 

cancel data "`infile`"

