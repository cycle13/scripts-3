#!/usr/bin/perl -w

# Description: Analyye log files to extract data
#
#       Usage: ./xxx.pl
#      Output: xxx.xxx
#
#      Author: OU Yuyuan <ouyuyuan@gmail.com>
#     Created: 2012-10-06 19:51:14 CST
# Last Change: 2013-03-30 18:33:21 CST

use strict;
use 5.010;
use Date::Calc qw( Today Delta_Days Add_Delta_Days );
use File::Basename;
use getConfig;

# def variables <<<1

my $mainname= "daily_log";
my $dat_dir = get_config("data directory")."/$mainname";
my $log_dir = "/home/ou/archive/org/log";
my $missing = -999;

my $outcdl  = "$dat_dir/$mainname.cdl";

my $nday    = Delta_Days(2012,10,1, Today());
my %days;

my $date;
my %job;
my $jobtag;

my %manga;
my %so;
my %fit;

my %jobid;
&get_jobid;
my @jobtags = sort { $jobid{$a} <=> $jobid{$b} } keys %jobid;

# generate dates <<<1
for (my $i=0; $i<=$nday; $i++) {
    my ($year, $month, $day) = Add_Delta_Days(2012, 10, 1, $i);
    $month = "0$month" if $month<10;
    $day   = "0$day" if $day<10;
    $days{"$year-$month-$day"} = $i+1;
}

my @dates = sort { $days{$a} <=> $days{$b} } keys %days;

# glob log files <<<1

chdir $log_dir;
for (glob 'w*.org') {
    push(@ARGV, $_) if /w\d+\.org/;
}

# check lines of logs <<<1

while (<>) {

    # extract job-tag <<<2

    if (/^\*+\s+    # org level
        .*?
        <(\d+):(\d+)>  # job tag
        /x) {
        $jobtag = "<$1:$2>";
        die "unknown job tag in $_ of $ARGV" unless $jobid{$jobtag};
    }

    # extract job time <<<2

    if (/^\s*CLOCK:\s*/) { # clock tag
        if (/^\s*CLOCK:\s*
            \[(?<jobdate>20\d\d-\d\d-\d\d)
            \s(?<week>\w{3})\s
            (?<bh>\d+):(?<bm>\d+)        # begin time
            .*?
            (?<eh>\d+):(?<em>\d+)        # begin time
            .*?(?<lt>\d+:\d+)\s*$        # lasted time
            /x) {
            my $bmin = $+{bh}*60 + $+{bm};
            my $emin = $+{eh}*60 + $+{em};
            for (my $m=$bmin; $m<$emin; $m++) {
                my $jobkey = "$+{jobdate}-$m";
                $job{$jobkey} = $jobid{$jobtag};
            }

        } else { 
            die "some thing wrong in clock in $_\n"; 
        }
    }

    # get date info <<<2

    # dates and total days since 2012-10-01
    if (/
        ^\*\s+                        # mark of top layer of org-mode
        <(20\d{2}-\d{2}-\d{2})        # date of log
        /x) {
        $date = $1;
    }

    # yes or no activity <<<2

    if (/
        ^\s*<
        (?<manga>\d),
        (?<doeso>\d),
        (?<fit>\d)
        >\s*$
        /x) {
        $manga{$date}     = $+{manga};
        $so{$date} = $+{doeso};
        $fit{$date}   = $+{fit};
    }
}

# write CDL file <<<1

my $main = (split(/./,$outcdl))[0];
my $nmin = 24*60;
open OUTCDL, ">$outcdl" or die "Can't open file: $!";
say OUTCDL "
netcdf $mainname {

dimensions:
    min = $nmin, day = $nday;

variables:
    int manga(day);
        manga:long_name = \"Reading Manga\";
        manga:_FillValue = $missing;

    int so(day);
        so:long_name = \"Doesotrue\";
        so:_FillValue = $missing;

    int fit(day);
        fit:long_name = \"Fitness\";
        fit:_FillValue = $missing;

    int job(day, min);
        job:long_name = \"job per minute\";
        job:_FillValue = $missing;

    :source = \"generated by: " . basename($0) . "\";
    :job_ids = \"" . &jobid_string . "\";

data:
    manga = " . &data_string(\%manga) . "
    so = "    . &data_string(\%so)    . "
    fit = "   . &data_string(\%fit)   . "
    job = "   . &jobs_string()        . "
}";
close(OUTCDL);

# subroutines <<<1

# get job id  <<<2

sub get_jobid {
    my $leva;
    my $levb;
    my $tag;
    my $njobs = 0;

    open IN, "<$log_dir/jobs.org";
    while(<IN>) {
        if (/^\*\s+
            .*?\s+
            :(\d+):     # first level
            /x) {
            $leva = $1;
        }
        if (/^\*\*\s+
            .*?\s+
            :(\d+):     # first level
            /x) {
            $levb = $1;
            $tag = "<$leva:$levb>";
            $jobid{$tag} = $njobs + 1;
            $njobs += 1;
        }
    }
}

# data to string <<<2

sub data_string {
    my $ref = shift;
    my $string;
    for (my $i=0; $i<=$nday; $i++) {
        my $suf = ",";
        $suf = ";" if $i==$nday;
        $string .= "\n" if $i % 10 == 0;
        $string .= $ref->{$dates[$i]}//$missing;
        $string .= $suf;
    }
    $string;
}

# jobs ids to string  <<<2

sub jobid_string {
    my $string;
    for my $tag ( @jobtags ) {
#        $string .= "$tag -> $jobid{$tag}\n\t\t\t\t";
        $string .= "$tag:$jobid{$tag}  ";
    }
    $string;
}

# jobs string <<<2

# return assignment string in CDL syntax
sub jobs_string {
    my $string;

    # per day
    for (my $i=0; $i<=$nday; $i++) {
        my $day = $dates[$i];

        # per minute
        for (my $k=0; $k<$nmin; $k++) {

            my $jobkey = "$day-$k";
            $string .= "\n" if $k % 10 == 0;
            $string .= $job{$jobkey}//$missing;
            my $suf = ",";
            # last data in assignment
            $suf = ";" if $i==$nday && $k==$nmin-1;
            $string .= $suf;
        }

        $string .= "\n";
    }

    $string;
}
